(RndDir
   (types
      (mtv_overlay
         (editor
            (show
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 50}}))
            (hide
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 0}}))
            (play
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim animate}})))
         (enter
            {$this dx_set_mtv_pos} ;set the custom position of the mtv_overlay on enter
            {set $dx_mtvup TRUE}
            {if
               {exists game}
               {game
                  add_sink
                  $this
                  (intro_end)}
               {$this setup_text}
               {if
                  {exists fade.anim}
                  {fade.anim set_frame {if_else {modifier_mgr is_modifier_active mod_songtitle} 50 0}} ; dx - if song title always is enabled set the current frame of the fade animation to 50, which is all the way on
               }
            }
         )
         (dx_set_mtv_pos
            {if_else {modifier_mgr is_modifier_active mod_fakejuke} ;check if fake jukebox mode is enabled
               {Group.grp set_local_pos 0 -30 -136} ;if it is, hard set the location of the mtv overlay for style
               {Group.grp set_local_pos $mtvpos_0 $mtvpos_1 $mtvpos_2} ;otherwise put it in the custom location
            }
         )
         (exit
            {if
               {exists game}
               {game remove_sink $this}})
         (intro_end
            {if
               {&&
                  {exists game}
                  {gamemode get show_mtv_overlay}
                  {exists fade.anim}}
               {$this setup_text}
               {if_else
                  {gamemode is_tour}
                  {script_task
                     kTaskSeconds
                     (delay 7.0)
                     (script
                        {fade.anim animate})}
               {if_else {modifier_mgr is_modifier_active mod_songtitle} ;if song title always is enabled
                  {fade.anim set_frame 50} ;set the current frame of the fade animation to 50, which is all the way on
                  {fade.anim animate}}}})
         (setup_text
            {do
               ($short_name
                  {meta_performer song})
               ($song_title
                  {{song_mgr get_meta_data {meta_performer song}} title}
               )
               MTV_WRITER_CHOOSER ; dx - this is where we run mtv overlay setup
            }
         )
      )
   )
)
