#include ../dx/track/dx_track_handlers.dta
#include ../dx/track/dx_track_callback.dta
#define TRACK_END_FRAME
(1920)
#define TRACK_PANEL_HANDLERS
(#ifdef HX_WII (unload_async TRUE) #endif
   (enter
      {game add_sink $this}
      {$this dx_set_scoreboard_pos}
      {$this dx_set_star_display_pos}
      DX_ADD_SINKS
      DX_NO_BRE_HANDLER
      DX_ANGLE_RESET_HANDLER ;reset the track angle just to be safe
      #ifndef HX_WII {unless $dx_texloadonce DX_TEXTURE_RESET_HANDLER} #endif ;reset rb3 textures if no textures have been loaded yet
      #ifndef HX_WII DX_TRACK_TEXTURE_HANDLER #endif ;here is where we setup the custom track
      {$this
         set_showing
         {!
            {gamemode in_mode practice}}})
   (exit
      DX_ANIMATED_TASK_KILLER
      {game remove_sink $this})
   (unison_hit)
   (set_track_out)
   (set_track_in)
   (animate_track
      ($start $end $period $units))
   (animate_track_out)
   (animate_track_out_fast)
   (animate_track_in_fast)
   (finish_load
      {$this set_showing FALSE}
      {$this set_track_out})
   (intro_start
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_in})
   (intro_skip
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_out})
   (on_reset
      {beatmatch
         foreach_active_player
         $m
         {$m on_new_track}})
   (on_extend
      {$this play_intro}
      {if
         {&& {exists gamemode}
            {||
               {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
               {gamemode in_mode qp_coop} {gamemode in_mode campaign}
               {gamemode in_mode party_shuffle} {gamemode in_mode tour}
            }
         }
         {{coop_track_panel find crowd_meter} set_showing {! {modifier_mgr is_modifier_active mod_no_crowd_meter}}}
         {{coop_track_panel find scoreboard} set_showing {! {modifier_mgr is_modifier_active mod_no_scoreboard}}}
      }
      {if
         {&&
            {exists gamemode}
            {gamemode get play_track_intro_sfx}}
         {$this play_seq track_beg.cue}}))
{new
   TrackPanel
   coop_track_panel
   (dx_set_scoreboard_pos
      {{{coop_track_panel find scoreboard} find scoreboard.grp} set_local_pos $scoreboardpos_0 $scoreboardpos_1 $scoreboardpos_2}
   )
   (dx_set_star_display_pos
      {unless {&& {! $star_displaypos_0} {! $star_displaypos_1} {! $star_displaypos_2}}
         {{{{coop_track_panel find scoreboard} find star_display} find stars.grp} set_local_pos $star_displaypos_0 $star_displaypos_1 $star_displaypos_2}
      }
   )
   ;(dx_set_crowd_meter_pos
   ;   {{coop_track_panel find crowd_meter} reset}
   ;   {{{coop_track_panel find crowd_meter} find crowd_meter_shell.grp} set_local_pos $crowd_meterpos_0 $crowd_meterpos_1 $crowd_meterpos_2}
   ;)
   (file
      "track/trackpanel.milo")
   TRACK_PANEL_HANDLERS}
{func
   get_track_panel
   {gamemode get track_panel}}
{set
   $playback_file
   ""}