{func
   dx_log_writer
   ($dx_log_writer)
   #ifndef HX_WII
   {do
      DX_GAMEMODE_CHECKER
      {if_else {file_exists LOG_FILE_PATH}
         {set $dx_log_reader {read_file LOG_FILE_PATH}}
         {set $dx_log_reader ("start of log")}
      }
      {if {== $dx_log_counter 3}
         {push_back $dx_log_reader "Game Entered three times, starting new log."}
         {write_file PREV_LOG_FILE_PATH $dx_log_reader}
         {set $dx_log_counter 0}
         {set $dx_log_reader ({sprint "Game Entered three times, log reset. Number of songs completed this session: " $dx_num_songs_this_session})}
      }
      {push_back $dx_log_reader $dx_log_writer}
      {write_file LOG_FILE_PATH
         {array
            $dx_log_reader
         }
      }
   }
   #endif
}
{func
   dx_discordrp_writer
   ($dx_discordrp_writer)
   #ifndef HX_WII
   {do
      {write_file DRP_FILE_PATH {array ($dx_discordrp_writer)}}
   }
   #endif
}
{func
   dx_modifier_reader
   ($dx_modifier_reader)
   #ifndef HX_WII
   {foreach $entry {read_file MOD_FILE_PATH}
      {if {== {elem $entry 0} {basename $dx_modifier_reader}}
         {if {== {elem {find $entry $dx_modifier_reader} 1} 1}
            {modifier_mgr toggle_modifier_enabled $dx_modifier_reader}
         }
      }
   }
   #endif
}
{func
   dx_mtv_formatter
   ($dx_mtv_addition)
   {if_else {== $dx_mtv_addition reset}
      {do 
         {set $dx_mtv_holder ()}
         {resize $dx_mtv_holder 0}
         {set $dx_mtv_elements 0}
      }
      {do
         {push_back $dx_mtv_holder $dx_mtv_addition}
         {set $dx_mtv_elements {+ $dx_mtv_elements 1}}
      }
   }
   ;{dx_log_writer $dx_mtv_addition}
}
{func
   dx_rank_var_finder
   ($instrument $var_rankvar $rank_threshold_0 $rank_threshold_1 $rank_threshold_2 $rank_threshold_3 $rank_threshold_4 $rank_threshold_5)
   {foreach $entry $var_rankvar
      {if {== {elem $entry 0} {basename rank}}
         {if {elem {find $entry $instrument} 1}
            {set $var_actual_rankvar
               {elem {find $entry $instrument} 1}
            }
         }
      }
      {cond
         ({< $var_actual_rankvar $rank_threshold_0}
            {set $var_instrument_rankvar 0})
         ({&& {>= $var_actual_rankvar $rank_threshold_0} {< $var_actual_rankvar $rank_threshold_1}}
            {set $var_instrument_rankvar 1})
         ({&& {>= $var_actual_rankvar $rank_threshold_1} {< $var_actual_rankvar $rank_threshold_2}}
            {set $var_instrument_rankvar 2})
         ({&& {>= $var_actual_rankvar $rank_threshold_2} {< $var_actual_rankvar $rank_threshold_3}}
            {set $var_instrument_rankvar 3})
         ({&& {>= $var_actual_rankvar $rank_threshold_3} {< $var_actual_rankvar $rank_threshold_4}}
            {set $var_instrument_rankvar 4})
         ({&& {>= $var_actual_rankvar $rank_threshold_4} {< $var_actual_rankvar $rank_threshold_5}}
            {set $var_instrument_rankvar 5})
         ({>= $var_actual_rankvar $rank_threshold_5}
            {set $var_instrument_rankvar 6})
      }
   }
   {switch $instrument
      (guitar {set $guitarrankvar $var_instrument_rankvar})
      (bass {set $bassrankvar $var_instrument_rankvar})
      (drum {set $drumrankvar $var_instrument_rankvar})
      (vocals {set $vocalsrankvar $var_instrument_rankvar})
      (band {set $bandrankvar $var_instrument_rankvar})
      (keys {set $keysrankvar $var_instrument_rankvar})
      (real_keys {set $real_keysrankvar $var_instrument_rankvar})
      (real_guitar {set $real_guitarrankvar $var_instrument_rankvar})
      (real_bass {set $real_bassrankvar $var_instrument_rankvar})
   }
}