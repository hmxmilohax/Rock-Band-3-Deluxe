#define DX_SEL_DIFF_HANDLES
(
   (dx_play_countdown_set_time
      ($words $int)
      {if $int
         {play_countdown_text.lbl set_showing TRUE}
         {play_countdown_text.lbl set text_token {sprintf "%s: %i" $words $int}}
         {synth play metronome_lo.cue}
      }
      {if {! $int}
         {play_countdown_text.lbl set_showing FALSE}
      }
   )
   (dx_update_set_count
      {switch {size $dx_playlist_array}
         (0 {total.lbl set text_token {sprint " "}})
         (1 {total.lbl set text_token {sprintf {localize dx_play_a_show_setlist_count} {size $dx_playlist_array}}})
         {total.lbl set text_token {sprintf {localize dx_play_a_show_setlist_counts} {size $dx_playlist_array}}}
      }
   )
   (lbl_maker
      ($label $x $y $text_size $align $content)
      {new BandLabel $label}
      {$label set resource_name "pentatonic_outline"}
      {$label set alt_style_enabled TRUE}
      {$label set alt_font_resource_name instrument_icons_small}
      {$label set markup TRUE}
      {$label set_showing TRUE}
      {$label set_local_scale 1 1 1}
      {$label set_local_pos $x 0 $y}
      {$label set set_token_fmt $content}
      {$label set text_size $text_size}
      {$label set alt_text_size 25}
      {$label set alt_z_offset -4}
      {$label set fit_type 2}
      {$label set alignment $align}
      {$label set width 600}
      {$label set height 40}
      {$label set alpha 1}
      {$label set color_override play_color.color}
      {career.grp add_object $label}
   )
   (dx_generate_lines_info
      {if {! {exists play_color.color}}
         {new UIColor play_color.color}
         {play_color.color set color {pack_color 1 1 1}}
      }
      {career.grp set_showing TRUE}
      {if {! {exists play_play_text.lbl}}
         {$this lbl_maker play_play_text.lbl -220 162 40 kTopLeft {sprint dx_play_a_show_info}}
      }
      {if {! {exists play_countdown_text.lbl}}
         {$this lbl_maker play_countdown_text.lbl 400 162 20 kTopRight {sprint " "}}
         {$this dx_play_countdown_set_time {localize play_a_show_waiting} 0}
      }
      {if {! {exists play_header.lbl}}
         {$this lbl_maker play_header.lbl -277 220 20 kTopLeft {sprint " "}}
      }
      {if {! {exists total.lbl}}
         {$this lbl_maker total.lbl 400 162 40 kTopRight {sprint "content1"}}
      }
      {if {! {exists line1.lbl}}
         {$this lbl_maker line1.lbl -220 110 30 kTopLeft {sprint "content1"}}
      }
      {if {! {exists line2.lbl}}
         {$this lbl_maker line2.lbl -220 85 30 kTopLeft {sprint "content2"}}
      }
      {if {! {exists line3.lbl}}
         {$this lbl_maker line3.lbl -220 60 30 kTopLeft {sprint "content3"}}
      }
      {if {! {exists line4.lbl}}
         {$this lbl_maker line4.lbl -220 35 30 kTopLeft {sprint "content4"}}
      }
      {if {! {exists line5.lbl}}
         {$this lbl_maker line5.lbl -220 10 30 kTopLeft {sprint "content5"}}
      }
      {if {! {exists inst1.lbl}}
         {$this lbl_maker inst1.lbl -230 110 30 kTopRight {sprint "content1"}}
      }
      {if {! {exists inst2.lbl}}
         {$this lbl_maker inst2.lbl -230 85 30 kTopRight {sprint "content2"}}
      }
      {if {! {exists inst3.lbl}}
         {$this lbl_maker inst3.lbl -230 60 30 kTopRight {sprint "content3"}}
      }
      {if {! {exists inst4.lbl}}
         {$this lbl_maker inst4.lbl -230 35 30 kTopRight {sprint "content4"}}
      }
      {if {! {exists inst5.lbl}}
         {$this lbl_maker inst5.lbl -230 10 30 kTopRight {sprint "content5"}}
      }
      {line1.lbl set alt_font_resource_name pentatonic_outline}
      {line2.lbl set alt_font_resource_name pentatonic_outline}
      {line3.lbl set alt_font_resource_name pentatonic_outline}
      {line4.lbl set alt_font_resource_name pentatonic_outline}
      {line1.lbl set alt_italics 0.1}
      {line2.lbl set alt_italics 0.1}
      {line3.lbl set alt_italics 0.1}
      {line4.lbl set alt_italics 0.1}
      {{dx_play_a_show_panel find career_description.lbl} set_showing FALSE}
      {{dx_play_a_show_panel find inst1.lbl} set_token_fmt {sprint "1 . "}}
      {{dx_play_a_show_panel find inst2.lbl} set_token_fmt {sprint "2. "}}
      {{dx_play_a_show_panel find inst3.lbl} set_token_fmt {sprint "3. "}}
      {{dx_play_a_show_panel find inst4.lbl} set_token_fmt {sprint "4. "}}
      {{dx_play_a_show_panel find inst5.lbl} set_token_fmt {sprint "5. "}}
      {if {! $gotthislabelpreview}
         {set $gotthislabelpreview {song_preview.lbl get_local_pos_index 2}}
      }
      {song_preview.lbl set_local_pos_index 2 {- $gotthislabelpreview 15}}
      {play_play_text.lbl set text_token dx_play_a_show_info}
   )
   (dx_check_for_inst_genre
      {if {! $play_a_show_found_genre_part}
         {set $play_a_show_found_genre_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if {! {{song_mgr get_meta_data $random_genre_song} has_part $entry}}
               {set $play_a_show_found_genre_part FALSE}
               {set $random_genre_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_year
      {if {! $play_a_show_found_year_part}
         {set $play_a_show_found_year_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if {! {{song_mgr get_meta_data $random_year_song} has_part $entry}}
               {set $play_a_show_found_year_part FALSE}
               {set $random_year_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_length
      {if {! $play_a_show_found_length_part}
         {set $play_a_show_found_length_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if {! {{song_mgr get_meta_data $random_length_song} has_part $entry}}
               {set $play_a_show_found_length_part FALSE}
               {set $random_length_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_song
      {if {! $play_a_show_found_song_part}
         {set $play_a_show_found_song_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if {! {{song_mgr get_meta_data $random_song} has_part $entry}}
               {set $play_a_show_found_song_part FALSE}
               {set $random_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_tempo
      {if {! $play_a_show_found_tempo_part}
         {set $play_a_show_found_tempo_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if
               {||
                  {== {{song_mgr get_meta_data $random_tempo_song} anim_tempo} 32}
                  {! {{song_mgr get_meta_data $random_tempo_song} has_part $entry}}
               }
               {set $play_a_show_found_tempo_part FALSE}
               {set $random_tempo_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_fem
      {if {! $play_a_show_found_fem_part}
         {set $play_a_show_found_fem_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if
               {||
                  {== {{song_mgr get_meta_data $random_fem_song} is_ugc} TRUE}
                  {!= {{song_mgr get_meta_data $random_fem_song} vocal_gender} female}
                  {! {{song_mgr get_meta_data $random_fem_song} has_part $entry}}
               }
               {set $play_a_show_found_fem_part FALSE}
               {set $random_fem_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_check_for_inst_ugc
      {if {! $play_a_show_found_ugc_part}
         {set $play_a_show_found_ugc_part TRUE}
         {foreach $entry $dx_play_a_show_active_instruments
            {if {! {{song_mgr get_meta_data $random_ugc_song} has_part $entry}}
               {set $play_a_show_found_ugc_part FALSE}
               {set $random_ugc_song {song_select_panel pick_a_song}}
            }
         }
      }
   )
   (dx_refesh_songs
      ;we actually roll for songs 30 times for each line
      ;boy i hope we find one with the instrument you use
      {set $numtimes ()}
      {resize $numtimes 30}

      {set $play_a_show_found_genre_part FALSE}
      {set $play_a_show_found_year_part FALSE}
      {set $play_a_show_found_length_part FALSE}
      {set $play_a_show_found_song_part FALSE}
      {set $play_a_show_found_tempo_part FALSE}
      {set $play_a_show_found_fem_part FALSE}
      {set $play_a_show_found_ugc_part FALSE}
      {if {session_mgr is_local}
         ;start the flow with a random songs
         {set $num {random_int 0 {{song_select_panel find song.lst} num_data}}}
         {song_select_panel highlight_node_at_ix $num}

         ;hopefully find a song
         {set $random_genre_song {song_select_panel pick_a_song}}
         {set $random_year_song {song_select_panel pick_a_song}}
         {set $random_length_song {song_select_panel pick_a_song}}
         {set $random_song {song_select_panel pick_a_song}}
         {set $random_tempo_song {song_select_panel pick_a_song}}
         {set $random_fem_song {song_select_panel pick_a_song}}
         {set $random_ugc_song {song_select_panel pick_a_song}}
         ;run checks on the songs x num of times
         {foreach $entry $numtimes
            {$this dx_check_for_inst_genre}
            {$this dx_check_for_inst_year}
            {$this dx_check_for_inst_length}
            {$this dx_check_for_inst_song}
            {$this dx_check_for_inst_tempo}
            {$this dx_check_for_inst_fem}
            {$this dx_check_for_inst_ugc}
         }
         ;go back to the top of the song list
         {{song_select_panel find song.lst} set_selected_simulate_scroll 1}

         ;populate found metadata
         {set $random_genre {{song_mgr get_meta_data $random_genre_song} genre}}
         {set $random_year {int {{song_mgr get_meta_data $random_year_song} year_released}}}
         {set $random_length {{song_mgr get_meta_data $random_length_song} length_ms}}
         {set $random_song_title {{song_mgr get_meta_data $random_song} title}}
         {set $random_tempo {{song_mgr get_meta_data $random_tempo_song} anim_tempo}}
         {set $random_fem {{song_mgr get_meta_data $random_fem_song} vocal_gender}}
         {set $random_ugc {{song_mgr get_meta_data $random_ugc_song} is_ugc}}
      }
   )
   (set_line
      ($lineeval $line)
      {switch $lineeval
         (genre
            {cond
               ({! $play_a_show_found_genre_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {set $dx_play_a_show_line {sprintf {localize play_a_show_genre} {localize $random_genre}}}
            }
         )
         (year
            {cond
               ({! $play_a_show_found_year_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {set $dx_play_a_show_line {sprintf {localize play_a_show_year} $random_year}}
            }
         )
         (length
            {cond
               ({! $play_a_show_found_length_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {if_else {> $random_length 420000}
                  {set $dx_play_a_show_line {localize play_a_show_really_long}}
                  {if_else {> $random_length 240000}
                     {set $dx_play_a_show_line {localize play_a_show_long}}
                     {if_else {> $random_length 120000}
                        {set $dx_play_a_show_line {localize play_a_show_short}}
                        {set $dx_play_a_show_line {localize play_a_show_really_short}}
                     }
                  }
               }
            }
         )
         (song
            {cond
               ({! $play_a_show_found_song_part}
                  {{sprint "line" $line ".lbl"} set_token_fmt {localize play_a_show_missing_inst}}
               )
               {if $play_a_show_found_song_part
                  {{sprint "line" $line ".lbl"} set_song_and_artist_name_from_sym $random_song 0}
                  {{sprint "line" $line ".lbl"} set alt_font_resource_name pentatonic_outline}
                  {{sprint "line" $line ".lbl"} set alt_italics 0.1}
               }
            }
         )
         (tempo
            {cond
               ({! $play_a_show_found_tempo_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {set $dx_play_a_show_line
                  {switch $random_tempo
                     (16 {localize play_a_show_slow})
                     (32 {localize play_a_show_med})
                     (64 {localize play_a_show_fast})
                     (112 {localize play_a_show_fast})
                  }
               }
            }
         )
         (fem
            {cond
               ({! $play_a_show_found_fem_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {set $dx_play_a_show_line {localize play_a_show_woman}}
            }
         )
         (ugc
            {cond
               ({! $play_a_show_found_ugc_part}
                  {set $dx_play_a_show_line {localize play_a_show_missing_inst}}
               )
               {set $dx_play_a_show_line {localize play_a_show_community}}
            }
         )
         (refresh
            {set $dx_play_a_show_line {localize play_a_show_refresh}}
         )
      }
      {if_else {== $lineeval song}
         {{dx_play_a_show_panel find {sprint "line" $line ".lbl"}} set_song_and_artist_name_from_sym $random_song 0}
         {{dx_play_a_show_panel find {sprint "line" $line ".lbl"}} set_token_fmt {sprint $dx_play_a_show_line}}
      }
   )
   (dx_pick_a_show
      {set $availarray (genre year tempo song length year song genre genre length song year ugc year genre song tempo length song)}
      {set $avail2array ()}
      {set $avail3array ()}
      {set $avail4array ()}
      {resize $avail2array 0}
      {resize $avail3array 0}
      {resize $avail4array 0}
      {set $line1eval {random_elem $availarray}}
      {foreach $entry $availarray
         {if {&& {!= $entry ""} {!= $entry $line1eval}}
            {push_back $avail2array $entry}
         }
      }
      {set $line2eval {random_elem $avail2array}}
      {foreach $entry $avail2array
         {if {&& {!= $entry ""} {!= $entry $line1eval} {!= $entry $line2eval}}
            {push_back $avail3array $entry}
         }
      }
      {set $line3eval {random_elem $avail3array}}
      {foreach $entry $avail3array
         {if {&& {!= $entry ""} {!= $entry $line1eval} {!= $entry $line2eval} {!= $entry $line3eval}}
            {push_back $avail4array $entry}
         }
      }
      {set $line4eval {random_elem $avail4array}}
      {set $line5eval refresh}
      {resize $avail2array 0}
      {resize $avail3array 0}
      {resize $avail4array 0}
      {$this set_line $line1eval 1}
      {$this set_line $line2eval 2}
      {$this set_line $line3eval 3}
      {$this set_line $line4eval 4}
      {$this set_line $line5eval 5}
      {unless {session_mgr is_local}
         {if {is_leader_local}
            {session send_msg_to_all
               {'`' 
                  (
                     {do
                        {$this set_line {',' $line1eval} 1}
                        {$this set_line {',' $line2eval} 2}
                        {$this set_line {',' $line3eval} 3}
                        {$this set_line {',' $line4eval} 4}
                        {$this set_line {',' $line5eval} 5}
                     }
                  )
                  kNetReliable
               }
            }
         }
      }
   )
   (handle_album_art
      {if {! $current_voted_song}
         {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
         {{dx_play_a_show_panel find song_preview.lbl} set_token_fmt dx_play_a_show_welcome}
      }
      {if_else {== $dx_play_a_show_vote 5}
         {do
            {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
            {song_preview.lbl set text_token {localize play_a_show_refreshed}}
         }
         {if $current_voted_song
            {play_header.lbl set text_token {localize play_a_show_bandvote}}
            #ifdef HX_XBOX
            {if_else {file_exists {song_mgr song_file_path {meta_performer song} "_keep.png_xbox"}}
               {$this update_preview_song $current_voted_song {song_mgr song_file_path {meta_performer song} "_keep.png"} 0}
               {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
            }
            #endif
            #ifdef HX_PS3
            {if_else {file_exists {song_mgr song_file_path {meta_performer song} "_keep.png_ps3"}}
               {$this update_preview_song $current_voted_song {song_mgr song_file_path {meta_performer song} "_keep.png"} 0}
               {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
            }
            #endif
            #ifdef HX_WII
            {if_else {file_exists {song_mgr song_file_path {meta_performer song} "_keep.png_wii"}}
               {$this update_preview_song $current_voted_song {song_mgr song_file_path {meta_performer song} "_keep.png"} 0}
               {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
            }
            #endif
         }
      }
   )
   (dx_play_a_show_info
      {set $dx_play_a_show_num_votes 0}
      {set $slot1_voted FALSE}
      {set $slot2_voted FALSE}
      {set $slot3_voted FALSE}
      {set $slot4_voted FALSE}
      {set $slot1_vote FALSE}
      {set $slot2_vote FALSE}
      {set $slot3_vote FALSE}
      {set $slot4_vote FALSE}
      
      {$this dx_refesh_songs}
      {$this dx_pick_a_show}
      {$this handle_album_art}
      {$this dx_update_set_count}
      {$this format_vote vote}
      
      {if $playintrotext
         {set $playintrotext FALSE}
         {play_header.lbl set text_token {sprint " "}}
      }
      {set $votingallowed TRUE}
      {set $dx_play_a_show_required_votes 0}
      {overshell begin_override_flow kOverrideFlow_None}
      {overshell begin_override_flow kOverrideFlow_SongSettings}
   )
   (calc_highest_vote
      {set $vote_check_array ()}
      {resize $vote_check_array 0}
      {if $slot1_voted {push_back $vote_check_array $slot1_vote}}
      {if $slot2_voted {push_back $vote_check_array $slot2_vote}}
      {if $slot3_voted {push_back $vote_check_array $slot3_vote}}
      {if $slot4_voted {push_back $vote_check_array $slot4_vote}}
      {set $vote_check_array {array $vote_check_array}}
      {set $1count 0}
      {set $2count 0}
      {set $3count 0}
      {set $4count 0}
      {set $5count 0}
      {set $max 0}
      {foreach $entry $vote_check_array
         {switch $entry
            (1 {set $1count {+ $1count 1}})
            (2 {set $2count {+ $2count 1}})
            (3 {set $3count {+ $3count 1}})
            (4 {set $4count {+ $4count 1}})
            (5 {set $5count {+ $5count 1}})
         }
      }
      {if {> $1count $max}
        {set $max $1count}
        {set $dx_play_a_show_vote 1}
      }
      {if {> $2count $max}
        {set $max $2count}
        {set $dx_play_a_show_vote 2}
      }
      {if {> $3count $max}
        {set $max $3count}
        {set $dx_play_a_show_vote 3}
      }
      {if {> $4count $max}
        {set $max $4count}
        {set $dx_play_a_show_vote 4}
      }
      {if {> $5count $max}
        {set $max $5count}
        {set $dx_play_a_show_vote 5}
      }
      {$this dx_play_a_show_send_voted}
   )
   (store_voted_song
      ($song)
      {set $current_voted_song $song}
      {meta_performer set_song $song}
      {push_back $dx_playlist_array $song}
   )
   #define DX_PLAY_SONG_PICKER
   (
      (genre
         {$this store_voted_song $random_genre_song}
      )
      (year
         {$this store_voted_song $random_year_song}
      )
      (length
         {$this store_voted_song $random_length_song}
      )
      (song
         {$this store_voted_song $random_song}
      )
      (tempo
         {$this store_voted_song $random_tempo_song}
      )
      (fem
         {$this store_voted_song $random_fem_song}
      )
      (ugc
         {$this store_voted_song $random_ugc_song}
      )
   )
   (hold_up
      {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
      {song_preview.lbl set text_token {localize play_a_show_refreshing}}
      {set $votingallowed FALSE}
      {set $current_voted_song FALSE}
      {play_countdown_text.lbl set_showing TRUE}
      {play_countdown_text.lbl set text_token {sprintf "%s: %i" {localize play_a_show_get_songs} 1}}
      {if {exists dx_hold_up}
         {delete dx_hold_up}
      }
      {script_task
         kTaskSeconds
         (name dx_hold_up)
         (delay 1)
         (script 
            {$this dx_play_countdown_set_time {localize play_a_show_waiting} 0}
            {$this dx_play_a_show_info})
      }
   )
   (dx_play_a_show_send_voted
      {$this format_vote band}
      {play_header.lbl set text_token {sprint " "}}
      {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
      {song_preview.lbl set text_token {localize play_a_show_pick_songs}}
      {switch $dx_play_a_show_vote
         (1 {switch $line1eval DX_PLAY_SONG_PICKER})
         (2 {switch $line2eval DX_PLAY_SONG_PICKER})
         (3 {switch $line3eval DX_PLAY_SONG_PICKER})
         (4 {switch $line4eval DX_PLAY_SONG_PICKER})
         (5
            {$this dx_refesh_songs}
            {$this update_preview_song $current_voted_song "dx/custom_textures/_additional_textures/blank_album_art_keep.png" 0}
            {song_preview.lbl set text_token {localize play_a_show_refreshing}}
         )
      }
      {set $dx_playlist_array {array $dx_playlist_array}}
      {set $votingallowed FALSE}
      {if {exists dx_send_vote1}
         {delete dx_send_vote1}
      }
      {if {exists dx_send_vote2}
         {delete dx_send_vote2}
      }
      {if {exists dx_send_vote3}
         {delete dx_send_vote3}
      }
      {$this dx_play_countdown_set_time {localize play_a_show_submitting} 3}
      {script_task kTaskSeconds (name dx_send_vote1) (delay 1)
         (script {$this dx_play_countdown_set_time {localize play_a_show_submitting} 2})
      }
      {script_task kTaskSeconds (name dx_send_vote2) (delay 2)
         (script {$this dx_play_countdown_set_time {localize play_a_show_submitting} 1})
      }
      {script_task kTaskSeconds (name dx_send_vote3) (delay 3)
         (script
            {$this dx_play_countdown_set_time {localize play_a_show_submitting} 0}
            {unless {== $dx_play_a_show_vote 5} {synth play career_rankup.cue}}
            {$this dx_play_a_show_info}
         )
      }
   )
   (format_vote
      ($type)
      {set $lines (1 2 3 4 5)}
      {switch $type
         (vote
            {foreach $line $lines
               {{dx_play_a_show_panel find {sprint "inst" $line ".lbl"}} set text_token 
                  {sprint
                     {if_else {&& $slot1_voted {== $slot1_vote $line}} {sprint "<alt>" $slot1_voted_icon "</alt>"} {sprint ""}}
                     {if_else {&& $slot2_voted {== $slot2_vote $line}} {sprint "<alt>" $slot2_voted_icon "</alt>"} {sprint ""}}
                     {if_else {&& $slot3_voted {== $slot3_vote $line}} {sprint "<alt>" $slot3_voted_icon "</alt>"} {sprint ""}}
                     {if_else {&& $slot4_voted {== $slot4_vote $line}} {sprint "<alt>" $slot4_voted_icon "</alt>"} {sprint ""}}
                     {if_else {== $line 1} {sprint "  "} {sprint ""}} $line {if_else {== $line 1} {sprint " . "} {sprint ". "}}
                  }
               }
            }
         )
         (band
            {foreach $line $lines
               {{dx_play_a_show_panel find {sprint "inst" $line ".lbl"}} set text_token
                  {sprint
                     {if_else {== $dx_play_a_show_vote $line}
                        {sprint "<alt>j</alt>"}
                        {sprint ""}
                     }
                     {if_else {== $line 1}
                        {sprint "  "}
                        {sprint ""}
                     }
                     $line
                     {if_else {== $line 1}
                        {sprint " . "}
                        {sprint ". "}
                     }
                  }
               }
            }
         )
      }
   )
   (dx_slot_voted
      {$this format_vote vote}
      {if {== $dx_play_a_show_num_votes $dx_play_a_show_required_votes}
         {$this dx_play_countdown_set_time {localize play_a_show_lock_in} 3}
         {if {exists dx_hold_vote1}
            {delete dx_hold_vote1}
         }
         {if {exists dx_hold_vote2}
            {delete dx_hold_vote2}
         }
         {if {exists dx_hold_vote3}
            {delete dx_hold_vote3}
         }
         {script_task kTaskSeconds (name dx_hold_vote1) (delay 1)
            (script {$this dx_play_countdown_set_time {localize play_a_show_lock_in} 2})
         }
         {script_task kTaskSeconds (name dx_hold_vote2) (delay 2)
            (script {$this dx_play_countdown_set_time {localize play_a_show_lock_in} 1})
         }
         {script_task kTaskSeconds (name dx_hold_vote3) (delay 3)
            (script
               {$this dx_play_countdown_set_time {localize play_a_show_lock_in} 0}
               {$this calc_highest_vote}
            )
         }
      }
   )
   (dx_play_a_show_recieve_song
      {if_else {> {size $dx_playlist_array} 0}
         {do
            {set $dx_in_show_flow FALSE}
            {meta_performer set_songs $dx_playlist_array}
            {set $dx_playing_a_show TRUE}
            {ui sync_screen {music_library get_next_screen} 0}
         }
         {do
            {dx_passive_messenger_symbol "no songs queued"}
         }
      }
   )
)