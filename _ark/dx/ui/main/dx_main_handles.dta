#define DX_MAIN_HANDLES
(
   (dx_main_on_enter
      #ifndef HX_WII
      DX_SET_SERVER_LOCALE ; dx - change button and label text when connected to ashcentral
      {dx_presence_manager}
      #endif
      {dx_sv_menu_logo_set sv3_panel}
      {set $dx_random_number {random_int 0 4096}}
      {if {== $dx_random_number 2112}
         {dx_passive_messenger you_lost}
      }
      {$this dx_check_birthday}
      {unless {== $dx_detected_platform platform_rpcs3}
         {$this disable mb_musicstore.btn}
         {mb_musicstore.btn set_showing FALSE}
      }
      #ifndef HX_PS3
      {content_mgr start_refresh}
      #endif
      {if {|| {! $screensaver} {! $gotcha} {modifier_mgr is_modifier_active mod_auto_play}}
         DX_MAIN_BUTTON_DISABLER
      }
      #ifndef HX_WII
      {set $dx_playing_a_show FALSE}
      {if {> {size {read_file DX_PLAYLIST_DTA_PATH}} 0}
         {$this dx_play_a_show}
      }
      #endif
   )
   (dx_unlock_pre_orders
      {switch {{$player get_user} get_slot_num}
         (0
            {main_hub_panel unlock_gamestop_award 0}
            {main_hub_panel unlock_x79_award 0}
            {main_hub_panel unlock_d2010_award 0}
         )
         (1
            {main_hub_panel unlock_gamestop_award 1}
            {main_hub_panel unlock_x79_award 1}
            {main_hub_panel unlock_d2010_award 1}
         )
         (2
            {main_hub_panel unlock_gamestop_award 2}
            {main_hub_panel unlock_x79_award 2}
            {main_hub_panel unlock_d2010_award 2}
         )
         (3
            {main_hub_panel unlock_gamestop_award 3}
            {main_hub_panel unlock_x79_award 3}
            {main_hub_panel unlock_d2010_award 3}
         )
      }
   )
   (dx_check_dx
      {unless {file_exists DX_SETTINGS_FILE_PATH}
         {unless $dxwelcomeseen
            {profile_mgr set_voice_chat_volume 0}
            {dx_log_writer info {sprint "Cannot find DX.dta"}}
            {ui push_screen hint_dx_welcome_screen}
         }
      }
   )
   (dx_check_xeniawrites
      {unless {file_exists DX_SETTINGS_FILE_PATH}
         {if $dxwelcomeseen
            {unless $dx_settings_error
               {dx_log_writer info {sprint "Cannot write DX.dta"}}
               {ui push_screen hint_relative_writes_screen}
            }
         }
      }
   )
   (dx_check_birthday
      ;($year $month $day $hour $minute $second)
      {get_date_time $year $month $day $hour $minute $second}
      {if
         {&&
            {! $dx_msg_birth}
            ;{> $year 2023}
            {== $month 10}
            {== $day 25}
         }
         {set $dx_msg_birth TRUE}
         {dx_passive_messenger_symbol "Happy Birthday Shiloh!"}
      }
   )
   (dx_play_a_show
      {set $dx_playlist_array ()}
      {resize $dx_playlist_array 0}
      {set $dx_playlist_song FALSE}
      {if {> {size {read_file DX_PLAYLIST_DTA_PATH}} 0}
         {foreach $entry {elem {read_file DX_PLAYLIST_DTA_PATH} 0}
            {push_back $dx_playlist_array $entry}
         }
         {set $dx_playlist_song {elem $dx_playlist_array 0}}
         {remove_elem $dx_playlist_array $dx_playlist_song}
         {if_else {> {size $dx_playlist_array} 0}
            {write_file DX_PLAYLIST_DTA_PATH {array ( $dx_playlist_array )}}
            {write_file DX_PLAYLIST_DTA_PATH $dx_playlist_array}
         }
      }
      {if $dx_playlist_song
         {meta_performer set_song $dx_playlist_song}
         {gamemode set_mode qp_coop}
         {set $dx_playing_a_show TRUE}
         {ui sync_screen {music_library get_next_screen} 0}
      }
      {unless $dx_playlist_song
         {dx_passive_messenger_symbol "no songs queued"}
         {if {! $dx_play_a_show_warn}
            {set $dx_play_a_show_warn TRUE}
            {dx_modal_messenger_symbol "Play a show is a highly alpha feature and requires the usage of an external tool to generate a playlist. this is placeholder text that should be updated later"}
         }
      }
   )
)