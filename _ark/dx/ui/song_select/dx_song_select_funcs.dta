{func
   dx_song_select_enter
   {dx_presence_manager}
   ;{dx_sv_menu_logo_set sv4_panel}
   {set $dx_trainer_real_guitar FALSE}
   {set $dx_trainer_real_bass FALSE}
   {set $dx_trainer_real_keys FALSE}
   {set $dx_trainer_real_drum FALSE}
   {set $dx_skip_game_setup FALSE}
   {if {! $dx_prep_show_flow}
      {set $dx_playing_a_show FALSE}
   }
   {if $dx_prep_show_flow
      {dx_modal_messenger dx_play_a_show_intro}
   }
   {switch {{$this get_user} get_controller_type}
      (kControllerGuitar {set $dx_tracked_trainer FALSE})
      (kControllerDrum {set $dx_tracked_trainer drum})
      (kControllerVocals {set $dx_tracked_trainer FALSE})
      (kControllerKeys {set $dx_tracked_trainer real_keys})
      (kControllerRealGuitar {set $dx_tracked_trainer real_guitar})
      {fail "No choose part view for this controller type"}
   }
   {overshell set_active_status kOvershellInShell}
   {overshell update_all}
   {unless $dx_song_select_noise {synth stop_all_sfx TRUE}} ; dx - removes the annoying noises in song select
   {if $dx_confirm_song_count
      {set $high_song_count 4500}
      {unless $rb3e_checked_song_count
         {set $rb3e_checked_song_count TRUE}
         {if {> {{song_select_panel find song.lst} num_data} $high_song_count}
            {dx_modal_messenger_symbol 'High song counts, especially with multiple players, have been known to cause instability issues after prolonged play-time. It is recommended to:\nStick to music video venues\nTurn off song transition cutscenes\nAvoid very long, 10 min+ songs\nOr lower your song count\nYou have been warned.\n\nTo disable this warning go to Deluxe Settings > Advanced > Functional Options > High Song Count Warning'}
         }
      }
   }
   {$this dx_diff_maker}
   ;{unless $gocentral_sanity_checked
   ;   {if {&& {! {session_mgr is_local}} {< {{song_select_panel find song.lst} num_data} 130}}
   ;      {set $gocentral_sanity_checked TRUE}
   ;      {dx_modal_messenger_symbol 'If you show up to custom online lobbies in 2023 without dlc I hope you find jesus'}
   ;   }
   ;}
}
{func
   dx_song_select_endurance
   {if {|| $dx_customizer $dx_auto_endurance}
      {script_task kTaskSeconds
         (delay {if_else {== $dx_auto_endurance dx_selected} 1.5 0})
         (script
            {if_else {== $dx_auto_endurance dx_random}
               {script_task kTaskSeconds
                  (delay 2)
                  (script
                     {song.lst refresh}
                     {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                     {script_task kTaskSeconds
                        (delay 2)
                        (script
                           {song.lst refresh}
                           {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                           {script_task kTaskSeconds
                              (delay 2)
                              (script
                                 {song.lst refresh}
                                 {song.lst set_selected_simulate_scroll {random_int 0 {song.lst num_data}}}
                                 {script_task kTaskSeconds
                                    (delay 2)
                                    (script
                                       {if_else {== {{music_library get_highlighted_node} get_node_type} kNodeSong}
                                          {do
                                             {gamemode set_mode qp_coop}
                                             {music_library select_highlighted_node {input_mgr get_user}}
                                          }
                                          {dx_song_select_endurance}
                                       }
                                    )
                                 }
                              )
                           }
                        )
                     }
                  )
               }
               {do
                  {gamemode set_mode qp_coop}
                  {music_library select_highlighted_node {input_mgr get_user}}
               }
            }
         )
      }
   }
}
#define DX_SONG_LIST_LEFT_ACTION
(
   ({&&
      {== $action kAction_Left}
      {!
         {song.lst is_scrolling}}}
   {unless
      [waiting_for_sort]
      {song.lst set_scroll_user $user}
      {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 1}}
      {set $pause_scroll_sound TRUE}
      {song.lst set_selected_simulate_scroll {- {song.lst selected_pos} 9}}
      {set $pause_scroll_sound FALSE}}) ; dx scroll the list up by 10 slots when pressing left dpad
)
#define DX_SONG_LIST_RIGHT_ACTION
(
   ({&&
      {== $action kAction_Right}
      {!
         {song.lst is_scrolling}}}
   {unless
      [waiting_for_sort]
      {song.lst set_scroll_user $user}
      {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 1}}
      {set $pause_scroll_sound TRUE}
      {song.lst set_selected_simulate_scroll {+ {song.lst selected_pos} 9}}
      {set $pause_scroll_sound FALSE}}) ; dx scroll the list down by 10 slots when pressing right dpad
)