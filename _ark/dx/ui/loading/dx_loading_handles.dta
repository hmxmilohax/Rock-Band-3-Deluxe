#define DX_LOADING_HANDLES
(
   (dx_load_tip_and_author_finder
      {unless $dx_song_data_processed
         {set $dx_song_data_processed TRUE}
         {if {&& $dx_author_finder_is_enabled {! {gamemode in_mode trainer}}}
            {dx_author_finder_var_reset}
            MTV_DTA_PREPPER
            {unless $dx_mtv_dta_reader_once
               {set $dx_mtv_dta_reader_once TRUE} ;no matter how many times this function gets called, we only need to read these dtas once because its very heavy
               MTV_DTA_READER
            }
         }
      }
   )
   (dx_loading_enter
      {dx_countdown_var_reset}
      {dx_midi_parser_var_reset}
      {set $dx_song_data_processed FALSE}
      {$this dx_load_tip_and_author_finder}
      {if_else {! {session_mgr is_local}}
         {set $dx_delay 2}
         {if_else $dx_song_delay
            {set $dx_delay 2}
            {set $dx_delay 0}
         }
      }
      {set $speedmod_top $leaderspeed}
   )
   (dx_set_up_loading_tip_label
      #ifndef HX_WII
      {{transition_fallback_panel find loading.lbl} set caps_mode kCapsModeNone} ; make label lowercase
      {{transition_fallback_panel find loading.lbl} set fixed_length 1000} ; increase the character count
      {{transition_fallback_panel find loading.lbl} set width 640} ; force a specific length
      {{transition_fallback_panel find loading.lbl} set fit_type kFitWrap} ; fit the text to the length and make a new line when reached
      {{transition_fallback_panel find loading.lbl} set alignment kTopCenter} ; keep the text below the loading emblem
      {{transition_fallback_panel find loading.lbl} set_local_pos_index 2 {- {{transition_fallback_panel find loading.lbl} get_locale_pos_index 2} 150}} ; set the y position to make it a lil prettier
      {if_else $dx_loading_tip_var
         {{transition_fallback_panel find loading.lbl} set_token_fmt {sprint $dx_loading_tip_var}} ; set the loading tip found in songs.dta
         {{transition_fallback_panel find loading.lbl} set text_token {localize {random_elem {elem DX_LOADING_TIPS $index}}}} ; set a random loading tip
      }
      {set $dx_loading_tip_var FALSE}
      #endif
   )
)