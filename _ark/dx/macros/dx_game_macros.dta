#define DX_VENUE_SETTER
(
   {dx_log_writer info {sprint "Setting Venue - Current Screen: " {ui current_screen} " - Gamemode: " $dx_gamemode}}
   {if
      {&&
         {||
            {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
            {gamemode in_mode qp_coop} {gamemode in_mode party_shuffle}
            {gamemode in_mode practice} {gamemode in_mode qp_practice}
         }
         {session_mgr is_leader_local} ;set venue only if host of the lobby
      }
      {if_else $dx_customizer
         {do
            {gamecfg set_venue none}
            {meta_performer set_venue none}
         }
         {if_else {== $force TRUE}
            {switch $dx_venue
               (small_venues {do {gamecfg set_venue {random_elem (SMALL_VENUES)}} {meta_performer set_venue {random_elem (SMALL_VENUES)}}})
               (big_venues {do {gamecfg set_venue {random_elem (BIG_VENUES)}} {meta_performer set_venue {random_elem (BIG_VENUES)}}})
               (arena_venues {do {gamecfg set_venue {random_elem (ARENA_VENUES)}} {meta_performer set_venue {random_elem (ARENA_VENUES)}}})
               (festival_venues {do {gamecfg set_venue {random_elem (FESTIVAL_VENUES)}} {meta_performer set_venue {random_elem (FESTIVAL_VENUES)}}})
               (venues_video {do {gamecfg set_venue {random_elem (VENUES_VIDEO)}} {meta_performer set_venue {random_elem (VENUES_VIDEO)}}})
               (random
                  {if_else {modifier_mgr is_modifier_active mod_enablemv}
                      {do
                         {gamecfg set_venue {random_elem (ALL_VENUES)}}
                         {meta_performer set_venue {random_elem (ALL_VENUES)}}
                      }
                      {do
                         {gamecfg set_venue {random_elem (VENUES)}}
                         {meta_performer set_venue {random_elem (VENUES)}}
                      }
                  }
               )
               (none
                  {if_else {&& {! {session_mgr is_local}} {is_leader_local}}
                     {do
                        {gamecfg set_venue {random_elem (VENUES)}}
                        {meta_performer set_venue {random_elem (VENUES)}}
                     }
                     {if {&& {session_mgr is_local} {is_leader_local}}
                        {gamecfg set_venue none}
                        {meta_performer set_venue none}
                     }
                  }
               )
               {do {gamecfg set_venue $dx_venue} {meta_performer set_venue $dx_venue}}
            }
            {if_else {modifier_mgr is_modifier_active mod_enablemv}
               {do
                  {gamecfg set_venue {random_elem (ALL_VENUES)}}
                  {meta_performer set_venue {random_elem (ALL_VENUES)}}
               }
               {do
                  {gamecfg set_venue {random_elem (VENUES)}}
                  {meta_performer set_venue {random_elem (VENUES)}}
               }
            }
         }
      }
      {dx_log_writer default {sprint "Venue set to " $dx_venue " - Forced: " $force}}
   }
)
#define DX_SWAP_VENUE_MID_SETLIST
(
   {if {modifier_mgr is_modifier_active mod_venue_swap}
      DX_VENUE_SETTER
      {prefab_mgr assign_prefabs_to_slots}
      {char_sync update_char_cache}
   }
)
#define DX_SWAP_CHAR_MID_SETLIST
(
   {if
      {&&
         {||
            {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
            {gamemode in_mode qp_coop} {gamemode in_mode party_shuffle}
            {gamemode in_mode practice} {gamemode in_mode qp_practice}
         }
         {session_mgr is_leader_local} ;set venue only if host of the lobby
      }
      {if {modifier_mgr is_modifier_active mod_char_swap}
         {prefab_mgr assign_prefabs_to_slots}
         {char_sync update_char_cache}
      }
   }
)
#define DX_TRACKSPEED_HANDLER
(
   (easy {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 2.4} $trackspeed} $speedmod})
   (medium {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 2.0} $trackspeed} $speedmod})
   (hard {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 1.6} $trackspeed} $speedmod})
   (expert {* {'/' 1.2 $trackspeed} $speedmod})
)