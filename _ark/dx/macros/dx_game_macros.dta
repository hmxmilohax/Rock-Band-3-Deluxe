#define DX_ALL_BOTS_ENABLED
(
   {&& 
      $dx_bot_1
      {&&
         {&&
            $dx_bot_2
            $dx_bot_3
         }
         {&&
            $dx_bot_4
            $dx_bot_5
         }
      }
   }
)
#define DX_AUTOPLAYER_HANDLER
(
   {if_else {modifier_mgr is_modifier_active mod_auto_play}
      {do
         {dx_log_writer {sprint "Setting Autoplayers - 1: " $dx_bot_1 " - 2: " $dx_bot_2 " - 3: " $dx_bot_3 " - 4: " $dx_bot_4 " - 5: " $dx_bot_5}}
         {if {>= {beatmatch num_active_players} 1}
            {{beatmatch active_player 0} set_auto_play $dx_bot_1}
         }
         {if {>= {beatmatch num_active_players} 2}
            {{beatmatch active_player 1} set_auto_play $dx_bot_2}
         }
         {if {>= {beatmatch num_active_players} 3}
            {{beatmatch active_player 2} set_auto_play $dx_bot_3}
         }
         {if {>= {beatmatch num_active_players} 4}
            {{beatmatch active_player 3} set_auto_play $dx_bot_4}
         }
         {if {>= {beatmatch num_active_players} 5}
            {{beatmatch active_player 4} set_auto_play $dx_bot_5}
         }
      }
      {do
         {beatmatch foreach_active_player $player
            {$player set_auto_play 0}
         }
      }
   }
)
#define DX_BOT_OVERDRIVE_HANDLER
(
   ;runs in beatmatcher, do not add log
   {if {&& {== $dx_bot_overdrive dx_on} {! DX_ALL_BOTS_ENABLED}}
      {do
         {set $dx_tmp FALSE}
         {beatmatch foreach_active_player $player
            {if {$player is_deploying} {set $dx_tmp TRUE}}
         }
         {if {&& {>= {beatmatch num_active_players} 1} $dx_bot_1}
            {set $player {beatmatch active_player 0}}
            {if $dx_tmp {$player deploy_if_possible}}
            {if {>= {$player band_energy} 1} {$player deploy_if_possible}}
         }
         {if {&& {>= {beatmatch num_active_players} 2} $dx_bot_2}
            {set $player {beatmatch active_player 1}}
            {if $dx_tmp {$player deploy_if_possible}}
            {if {>= {$player band_energy} 1} {$player deploy_if_possible}}
         }
         {if {&& {>= {beatmatch num_active_players} 3} $dx_bot_3}
            {set $player {beatmatch active_player 2}}
            {if $dx_tmp {$player deploy_if_possible}}
            {if {>= {$player band_energy} 1} {$player deploy_if_possible}}
         }
         {if {&& {>= {beatmatch num_active_players} 4} $dx_bot_4}
            {set $player {beatmatch active_player 3}}
            {if $dx_tmp {$player deploy_if_possible}}
            {if {>= {$player band_energy} 1} {$player deploy_if_possible}}
         }
         {if {&& {>= {beatmatch num_active_players} 5} $dx_bot_5}
            {set $player {beatmatch active_player 4}}
            {if $dx_tmp {$player deploy_if_possible}}
            {if {>= {$player band_energy} 1} {$player deploy_if_possible}}
         }
      }
   }
)
#define DX_BOT_OVERDRIVE_DEPLETION_HANDLER
(
   ; this is likely the single dumbest thing in the whole codebase
   ;runs in beatmatcher, do not add log
   {if {== $dx_bot_overdrive dx_drain}
      {beatmatch foreach_active_player $player {$player empty_band_energy}}
   }
)
#define DX_DRUM_OD_DEPLOY_HANDLER
(
   ;runs in beatmatcher, do not add log
   {if_else {&& {== $dx_bot_overdrive dx_on} {&& {modifier_mgr is_modifier_active mod_auto_play} DX_ALL_BOTS_ENABLED}}
      {beatmatch foreach_active_player $player {unless {mod $measure 4} {$player deploy_if_possible}}}
      {if {modifier_mgr is_modifier_active mod_static_fills}
         {beatmatch foreach_active_player $player
            {if {== {$player instrument} drum}
               {$player deploy_if_possible}
            }
         }
      }
   }
)
#define DX_VENUE_SETTER
(
   {dx_log_writer {sprint "Setting Venue - Current Screen: " {ui current_screen} " - Gamemode: " $dx_gamemode}}
   {if
      {||
         {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
         {gamemode in_mode qp_coop} {gamemode in_mode party_shuffle}
         {gamemode in_mode practice} {gamemode in_mode qp_practice}
      }
      {if_else {== $force TRUE} ;prioritizes a forced venue over the no music video modifier
         {if_else {== $venue small_venues}
            {do
               {gamecfg set_venue {random_elem (SMALL_VENUES)}}
               {meta_performer set_venue {random_elem (SMALL_VENUES)}}
            }
            {if_else {== $venue big_venues}
               {do
                  {gamecfg set_venue {random_elem (BIG_VENUES)}}
                  {meta_performer set_venue {random_elem (BIG_VENUES)}}
               }
               {if_else {== $venue arena_venues}
                  {do
                     {gamecfg set_venue {random_elem (ARENA_VENUES)}}
                     {meta_performer set_venue {random_elem (ARENA_VENUES)}}
                  }
                  {if_else {== $venue festival_venues}
                     {do
                        {gamecfg set_venue {random_elem (FESTIVAL_VENUES)}}
                        {meta_performer set_venue {random_elem (FESTIVAL_VENUES)}}
                     }
                     {if_else {== $venue venues_video}
                        {do
                           {gamecfg set_venue {random_elem (VENUES_VIDEO)}}
                           {meta_performer set_venue {random_elem (VENUES_VIDEO)}}
                        }
                        {if_else {&& {== $venue random} {! {modifier_mgr is_modifier_active mod_nomv}}}
                           {do
                              {gamecfg set_venue {random_elem (ALL_VENUES)}}
                              {meta_performer set_venue {random_elem (ALL_VENUES)}}
                           }
                           {if_else {&& {== $venue random} {modifier_mgr is_modifier_active mod_nomv} {session_mgr is_leader_local}}
                              {do
                                 {gamecfg set_venue {random_elem (VENUES)}}
                                 {meta_performer set_venue {random_elem (VENUES)}}
                              }
                              {do
                                 {gamecfg set_venue $venue}
                                 {meta_performer set_venue $venue}
                              }
                           }
                        }
                     }
                  }
               }
            }
         }
         {if_else {&& {modifier_mgr is_modifier_active mod_nomv} {session_mgr is_leader_local}}
            {do
               {gamecfg set_venue {random_elem (VENUES)}}
               {meta_performer set_venue {random_elem (VENUES)}}
            }
            {do
               {gamecfg set_venue {random_elem (ALL_VENUES)}}
               {meta_performer set_venue {random_elem (ALL_VENUES)}}
            }
         }
      }
      {dx_log_writer {sprint "Venue set to " $venue " - Forced: " $force}}
   }
)
#define DX_GAMEMODE_CHECKER
(
   {if_else {exists gamemode}
      {do
         {if {gamemode in_mode init} {set $dx_gamemode init}}
         {if {gamemode in_mode campaign} {set $dx_gamemode campaign}}
         {if {gamemode in_mode defaults} {set $dx_gamemode defaults}}
         {if {gamemode in_mode tour} {set $dx_gamemode tour}}
         {if {gamemode in_mode qp_career_songinfo} {set $dx_gamemode qp_career_songinfo}}
         {if {gamemode in_mode qp_party_shuffle} {set $dx_gamemode qp_party_shuffle}}
         {if {gamemode in_mode practice} {set $dx_gamemode practice}}
         {if {gamemode in_mode qp_coop} {set $dx_gamemode qp_coop}}
         {if {gamemode in_mode party_shuffle} {set $dx_gamemode party_shuffle}}
         {if {gamemode in_mode qp_practice} {set $dx_gamemode qp_practice}}
         {if {gamemode in_mode pro_song_lessons_keyboard} {set $dx_gamemode pro_song_lessons_keyboard}}
         {if {gamemode in_mode pro_song_lessons_real_guitar} {set $dx_gamemode pro_song_lessons_real_guitar}}
         {if {gamemode in_mode pro_song_lessons_real_bass} {set $dx_gamemode pro_song_lessons_real_bass}}
         {if {gamemode in_mode drum_freestyle} {set $dx_gamemode drum_freestyle}}
         {if {gamemode in_mode trainer_drums} {set $dx_gamemode trainer_drums}}
         {if {gamemode in_mode trainer_pro_drums} {set $dx_gamemode trainer_pro_drums}}
         {if {gamemode in_mode trainer_real_guitar} {set $dx_gamemode trainer_real_guitar}}
         {if {gamemode in_mode trainer_pro_keyboard} {set $dx_gamemode trainer_pro_keyboard}}
         {if {gamemode in_mode trainer} {set $dx_gamemode trainer}}
         #ifdef HX_XBOX {if {gamemode in_mode audition} {set $dx_gamemode audition}} #endif
      }
      {set $dx_gamemode none}
   }
)
#define DX_LEADER_SPEED_SETTER
(
   {if {is_leader_local} ; dx - sync speeds only if currently the lobby leader
      {dx_log_writer {sprint "Sending Leader Speed to peers, Leaderspeed: " $leaderspeed " - Current Screen: " {ui current_screen} " - Gamemode: " $dx_gamemode}}
      DX_DISCONNECT
      {set $speedmod $leaderspeed}
      {set $speedmod_top $leaderspeed}
      {session send_msg_to_all {'`' (set $speedmod  {',' $leaderspeed}) kNetReliable}}
   }
)
#define DX_SET_SPEEDS
(
   {if {exists gamemode}
      ;{dx_log_writer {sprint "Setting up song speeds. Song Speed: " $speedmod}} ;runs in beatmatcher, do not enable under penalty of stutter
      {unless
         {|| ;check for a variety of game modes where song speed is not allowed to be adjusted
            {gamemode in_mode pro_song_lessons_keyboard} {gamemode in_mode pro_song_lessons_real_guitar}
            {gamemode in_mode pro_song_lessons_real_bass} {gamemode in_mode drum_freestyle}
            {gamemode in_mode trainer} {gamemode in_mode trainer_drums} {gamemode in_mode trainer_pro_drums}
            {gamemode in_mode trainer_real_guitar} {gamemode in_mode trainer_pro_keyboard}
            #ifdef HX_XBOX {gamemode in_mode audition} #endif ;audition is xbox only, ps3 will crash if you check for it
            {&& {gamemode in_mode practice} {> {gamecfg get practice_speed} 0}} ;if we are in practice and selected something other than 100% speed
         }
         {if {is_leader_local} {session send_msg_to_all {` (beatmatch set_music_speed {, $speedmod}) kNetReliable}}}
         {beatmatch set_music_speed $speedmod} ;apply the current selected speed in game
      }
      {if {&& {gamemode in_mode practice} {> {gamecfg get practice_speed} 0}} ;if we are in practice and selected something other than 100% speed
         {practice_panel update_speed {gamecfg get practice_speed}}
      }
   }
)
#define DX_TRACKSPEED_HANDLER
(
   (easy {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 2.4} $trackspeed} $speedmod})
   (medium {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 2.0} $trackspeed} $speedmod})
   (hard {* {'/' {if_else {modifier_mgr is_modifier_active mod_synced_track_speeds} 1.2 1.6} $trackspeed} $speedmod})
   (expert {* {'/' 1.2 $trackspeed} $speedmod})
)
#define DX_PLAYER_SINKS
(
   {$player add_sink fc_callback (hit miss pass check_fc check_missed num_gems_hit num_gems_combo num_gems_miss num_gems_pass)}
)
#define ACTIVE_PLAYER_RESET_HANDLER
(
   {set $bassplayeractive false}
   {set $vocalsplayeractive false}
   {set $real_guitarplayeractive false}
   {set $real_bassplayeractive false}
   {set $keysplayeractive false}
   {set $real_keysplayeractive false}
   {set $guitarplayeractive false}
   {set $drumplayeractive false}
   {set $drumplayeractive false}
)
#define DX_CHECK_ACTIVE_PLAYERS
(
   {do
      {beatmatch foreach_active_player $player ;dx - add our custom fc checking callbacks to the currently loaded player
         {switch {$player instrument}
            (bass {do {set $bassplayeractive true}})
            (real_guitar {do {set $real_guitarplayeractive true}})
            (real_bass {do {set $real_bassplayeractive true}})
            (keys {do {set $keysplayeractive true}})
            (real_keys {do {set $real_keysplayeractive true}})
            (guitar {do {set $guitarplayeractive true}})
            (drum {do {set $drumplayeractive true}})
            (real_drum {do {set $drumplayeractive true}})
            (vocals {set $vocalsplayeractive true})
         }
      }
   }
)
#define DX_ADD_SINKS
(
   {beatmatch foreach_active_player $player ;dx - add our custom fc checking callbacks to the currently loaded player
      {switch {$player instrument}
         (bass {do {set $bassplayeractive true} DX_PLAYER_SINKS})
         (real_guitar {do {set $real_guitarplayeractive true} DX_PLAYER_SINKS})
         (real_bass {do {set $real_bassplayeractive true} DX_PLAYER_SINKS})
         (keys {do {set $keysplayeractive true} DX_PLAYER_SINKS})
         (real_keys {do {set $real_keysplayeractive true} DX_PLAYER_SINKS})
         (guitar {do {set $guitarplayeractive true} DX_PLAYER_SINKS})
         (drum {do {set $drumplayeractive true} DX_PLAYER_SINKS})
         (real_drum {do {set $drumplayeractive true} DX_PLAYER_SINKS})
         (vocals {set $vocalsplayeractive true})
      }
   }
)
#define DX_GAME_RESET_HANDLER
(
   {dx_log_writer {sprint "Clearing Song Variables" " - Current Screen: " {ui current_screen}}}
   ;clears out the variables used for song information, sinks, and callbacks to be populated again
   {set $guitar_milosong FALSE}
   {set $bass_milosong FALSE}
   {set $real_guitar_milosong FALSE}
   {set $real_drum_milosong FALSE}
   {set $real_bass_milosong FALSE}
   {set $keys_milosong FALSE}
   {set $real_keys_milosong FALSE}
   {set $drum_milosong FALSE}
   ;callbacks
   {set $real_guitar_caughtmissed FALSE}
   {set $real_bass_caughtmissed FALSE}
   {set $real_drum_caughtmissed FALSE}
   {set $real_keys_caughtmissed FALSE}
   {set $keys_caughtmissed FALSE}
   {set $guitar_caughtmissed FALSE}
   {set $bass_caughtmissed FALSE}
   {set $drum_caughtmissed FALSE}
   ;callbacks
   {set $bass_firstnote FALSE}
   {set $guitar_firstnote FALSE}
   {set $real_bass_firstnote FALSE}
   {set $real_drum_firstnote FALSE}
   {set $real_guitar_firstnote FALSE}
   {set $keys_firstnote FALSE}
   {set $real_keys_firstnote FALSE}
   {set $drum_firstnote FALSE}
   ;callbacks
   {set $bass_groove_active FALSE}
   {set $drum_groove_active FALSE}
   {set $guitar_groove_active FALSE}
   ;acceleration mode
   {set $speedmod_top $leaderspeed}
   ;endgame sound effects
   {set $dx_someone_fcd FALSE}
   {set $dx_someone_niced FALSE}
   {set $dx_someone_jurgend FALSE}
)
#define DX_MTV_RESET_HANDLER
(
   {dx_log_writer {sprint "Clearing MTV Variables - Current Screen: " {ui current_screen}}}
   ;mtv
   {set $song_title FALSE}
   {set $authorvar FALSE}
   {set $artistvar FALSE}
   {set $yearvar FALSE}
   {set $albumvar FALSE}
   {set $dx_readdtaonce FALSE}
   {set $dx_mtv_dta_reader_once FALSE}
   {set $dx_mtv_written_once FALSE}
   {set $dx_mtv_read_once FALSE}
   {set $dx_hmxcheckedonce FALSE}
   {set $dx_author_finder_failed FALSE}
   {set $dx_path_failed FALSE}
   {set $guitarrankvar -1}
   {set $rankvar 0}
   {set $mastervar TRUE}
   {set $drumrankvar -1}
   {set $bassrankvar -1}
   {set $vocalsrankvar -1}
   {set $keysrankvar -1}
   {set $real_guitarrankvar -1}
   {set $real_bassrankvar -1}
   {set $real_keysrankvar -1}
   {set $bandrankvar -1}
)
#define DX_TEXTURE_RESET_HANDLER
(
   {dx_log_writer {sprint "Resetting Custom Textures to Default - Current Screen: " {ui current_screen}}}
   {if {== $smasher rb3} {set $smasher none}}

   {if {== $gem rb3} {set $gem none}}

   {if {== $sustain rb3} {set $sustain none}}

   {if {== $crowdmeter rb3} {set $crowdmeter none}}

   {if {== $multiring rb3} {set $multiring none}}

   {if {== $odbar rb3} {set $odbar none}}

   {if {== $rails rb3} {set $rails none}}

   {if {== $scoretex rb3} {set $scoretex none}}

   {if {== $flames rb3} {set $flames none}}

   {if {== $voxarrow rb3} {set $voxarrow none}}

   {if {== $voxhw rb3} {set $voxhw none}}

   {if {== $keyboard original} {set $keyboard none}}

   {if {== $highwaygtr _rb3_guitar} {set $highwaygtr none}}
   {if {== $highwaybass _rb3_bass} {set $highwaybass none}}
   {if {== $highwaydrum _rb3_drum} {set $highwaydrum none}}
   {if {== $highwaykeys _rb3_keys} {set $highwaykeys none}}

   {if {== $spotlightgtr _spotlight_guitar_track} {set $spotlightgtr none}}
   {if {== $spotlightbass _spotlight_bass_track} {set $spotlightbass none}}
   {if {== $spotlightdrum _spotlight_drums_track} {set $spotlightdrum none}}
   {if {== $spotlightkeys _spotlight_prokeys_track} {set $spotlightkeys none}}

   {if {== $emissivegtr rb3_guitar} {set $emissivegtr none}}
   {if {== $emissivebass rb3_bass} {set $emissivebass none}}
   {if {== $emissivedrum rb3_drums} {set $emissivedrum none}}
   {if {== $emissivekeys rb3_keys} {set $emissivekeys none}}

   {if {== $dx_multiangle 1} {set $dx_forcedx_multiangle FALSE}}
)