{func
   dx_track_theme_name
   ($dx_dta_name)
   #ifdef HX_XBOX {sprint "GAME:/dx_track_theme_" $dx_dta_name ".dta"} #endif
   #ifdef HX_PS3 {sprint "GD:/dev_hdd0/game/BLUS30463/USRDIR/dx_track_theme_" $dx_dta_name ".dta"} #endif
}
#define EXTERNAL_CUSTOM_TEX_PATH_FINDER
(
   #ifdef HX_PS3
   {sprint "GD:/dev_hdd0/game/BLUS30463/USRDIR/custom_textures/" $path "/gen/" $customtex ".png_ps3"}
   #else
   {sprint "GAME:/custom_textures/" $path "/gen/" $customtex ".png_xbox"}
   #endif
)
#define EXTERNAL_CUSTOM_TEX_PATH
(
   {$sourcetex set_bitmap {sprint #ifdef HX_PS3 "GD:/dev_hdd0/game/BLUS30463/USRDIR/custom_textures/" #else "GAME:/custom_textures/" #endif $path "/" $customtex ".png"}}
)
#define INTERNAL_CUSTOM_TEX_PATH_FINDER
(
   {sprint "dx/custom_textures/" $path "/gen/" $customtex #ifdef HX_PS3 ".png_ps3" #else ".png_xbox" #endif}
)
#define EXTERNAL_CUSTOM_TEX_PATH_FINDER_CATEGORY
(
   #ifdef HX_PS3
   {sprint "GD:/dev_hdd0/game/BLUS30463/USRDIR/custom_textures/" $category "/" $path "/gen/" $customtex ".png_ps3"}
   #else
   {sprint "GAME:/custom_textures/" $category "/" $path "/gen/" $customtex ".png_xbox"}
   #endif
)
#define EXTERNAL_CUSTOM_TEX_PATH_CATEGORY
(
   {$sourcetex set_bitmap {sprint #ifdef HX_PS3 "GD:/dev_hdd0/game/BLUS30463/USRDIR/custom_textures/" #else "GAME:/custom_textures/" #endif $category "/" $path "/" $customtex ".png"}}
)
#define INTERNAL_CUSTOM_TEX_PATH_FINDER_CATEGORY
(
   {sprint "dx/custom_textures/" $category "/" $path "/gen/" $customtex #ifdef HX_PS3 ".png_ps3" #else ".png_xbox" #endif}
)
{func
   dx_custom_texture_setter
   ($category $path $sourcetex $customtex)
   {dx_log_writer insane {sprint "func: dx_custom_texture_setter - Current Screen: " {ui current_screen} " - Gamemode: " $category ", " $path ", " $sourcetex ", " $customtex}}
   {dx_log_writer insane
      {sprintf "Executed dx_custom_texture_setter - time: %.4fms"
         {time
            {search_replace $customtex "'" "" $customtex}
            {set $dx_failover_tex FALSE}
            {unless {== $customtex none} ;check the texture is not set to default
               {dx_log_writer insane {sprint "Setting " $sourcetex " to: " $customtex}}
               {if_else $category
                  {if_else {file_exists EXTERNAL_CUSTOM_TEX_PATH_FINDER_CATEGORY}
                     EXTERNAL_CUSTOM_TEX_PATH_CATEGORY
                     {if_else {file_exists INTERNAL_CUSTOM_TEX_PATH_FINDER_CATEGORY}
                        {$sourcetex set_bitmap {sprint "dx/custom_textures/" $category "/" $path "/" $customtex ".png"}}
                        {set $dx_failover_tex TRUE}
                     }
                  }
                  {if_else {file_exists EXTERNAL_CUSTOM_TEX_PATH_FINDER}
                     EXTERNAL_CUSTOM_TEX_PATH
                     {if_else {file_exists INTERNAL_CUSTOM_TEX_PATH_FINDER}
                        {$sourcetex set_bitmap {sprint "dx/custom_textures/" $path "/" $customtex ".png"}}
                        {set $dx_failover_tex TRUE}
                     }
                  }
               }
               {if $dx_failover_tex
                  {if_else $category
                     {$sourcetex set_bitmap {sprint "dx/custom_textures/" $category "/" $path "/rock band 3.png"}}
                     {$sourcetex set_bitmap {sprint "dx/custom_textures/" $path "/rock band 3.png"}}
                  }
               }
            }
         }
      }
   }
}
