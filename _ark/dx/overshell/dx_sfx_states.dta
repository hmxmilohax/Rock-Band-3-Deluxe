#define DX_AUDIO_SFX_DATA
(
   {do
      ($options
         {array 0})
      {push_back $options os_fcaudio}
      {push_back $options {if_else $dx_song_select_noise os_audio_ambience_on os_audio_ambience_off}}
      {push_back $options {if_else $dx_instrument_fx os_audio_instsfx_on os_audio_instsfx_off}}
      {push_back $options {if_else $dx_jurgen_sound os_jurgen_sound_on os_jurgen_sound_off}}
      {push_back $options {if_else $dx_nice_sound os_nice_sound_on os_nice_sound_off}}
      {push_back $options
         {switch $dx_mic_deploy_sens 
            (0.9 os_dx_mic_od_1)
            (0.7 os_dx_mic_od_2)
            (0.5 os_dx_mic_od_3)
            (0.3 os_dx_mic_od_4)
            (0.1 os_dx_mic_od_5)
         }
      }
      ;-----func----------state name------message
      {dx_state_setup dxState_AudioSFX FALSE}
   }
)
(dxState_AudioSFX
   (view
      DX_AUDIO_SFX_DATA
      {overshell_view_chooser}
   )
   (SELECT_MSG
      {switch {$component selected_sym}
         ((os_jurgen_sound_on os_jurgen_sound_off)
            {set $dx_jurgen_sound {! $dx_jurgen_sound}}
            {if_else $dx_jurgen_sound
               {set $dx_tracked_changed os_jurgen_sound_on}
               {set $dx_tracked_changed os_jurgen_sound_off}
            }
            {dx_state_pos_tracker}
            DX_AUDIO_SFX_DATA
            {set $previous_state dxState_AudioSFX}
         )
         ((os_audio_ambience_on os_audio_ambience_off)
            {set $dx_song_select_noise {! $dx_song_select_noise}}
            {if_else $dx_song_select_noise
               {set $dx_tracked_changed os_audio_ambience_on}
               {set $dx_tracked_changed os_audio_ambience_off}
            }
            {dx_state_pos_tracker}
            DX_AUDIO_SFX_DATA
            {set $previous_state dxState_AudioSFX}
         )
         ((os_audio_instsfx_on os_audio_instsfx_off)
            {set $dx_instrument_fx {! $dx_instrument_fx}}
            {if_else $dx_instrument_fx
               {set $dx_tracked_changed os_audio_instsfx_on}
               {set $dx_tracked_changed os_audio_instsfx_off}
            }
            {dx_state_pos_tracker}
            DX_AUDIO_SFX_DATA
            {set $previous_state dxState_AudioSFX}
         )
         ((os_nice_sound_on os_nice_sound_off)
            {set $dx_nice_sound {! $dx_nice_sound}}
            {if_else $dx_nice_sound
               {set $dx_tracked_changed os_nice_sound_on}
               {set $dx_tracked_changed os_nice_sound_off}
            }
            {dx_state_pos_tracker}
            DX_AUDIO_SFX_DATA
            {set $previous_state dxState_AudioSFX}
         )
         (os_cyclefx
            {cycle_guitar_fx})
         (os_fcaudio
            {$this show_state dxState_FullComboSFX})
         ((os_dx_mic_od_1 os_dx_mic_od_2 os_dx_mic_od_3 os_dx_mic_od_4 os_dx_mic_od_5)
            {set $dx_mic_deploy_sens
               {switch $dx_mic_deploy_sens
                  (0.9 0.7)
                  (0.7 0.5)
                  (0.5 0.3)
                  (0.3 0.1)
                  (0.1 0.9)
               }
            }
            {set $dx_tracked_changed
               {switch $dx_mic_deploy_sens
                  (0.9 os_dx_mic_od_1)
                  (0.7 os_dx_mic_od_2)
                  (0.5 os_dx_mic_od_3)
                  (0.3 os_dx_mic_od_4)
                  (0.1 os_dx_mic_od_5)
               }
            }
            {dx_state_pos_tracker}
            DX_AUDIO_SFX_DATA
            {set $previous_state dxState_AudioSFX}
         )
      }
      {set $in_state FALSE}
      DX_AUDIO_SFX_DATA
      {set $previous_state dxState_AudioSFX}
   )
   (exit 
      {dx_state_pos_tracker}
      DX_OS_CLOSE_MSG
   )
   (on_cancel
      {$this show_state dxState_DeluxeSettings}
   )
)

(dxState_FullComboSFX
   (view
      {do
         ($options
            {array 0})
         {push_back $options os_nobg}
         {foreach $entry {file_list "sfx/streams/fc/*.mogg"} {if {!= $entry ""} {push_back $options $entry}}}
         {dx_state_setup dxState_FullComboSFX FALSE}
      }
      {overshell_view_chooser}
   )
   (SELECT_MSG
      {unless {== {$component selected_sym} os_nobg}
         {set $dx_fullcombo_sound {$component selected_sym}}
         {if_else {exists fc_preview_clip}
            {do
               {delete fc_preview_clip}
               {new MoggClip fc_preview_clip}
            }
            {new MoggClip fc_preview_clip}
         }
         {fc_preview_clip set file {sprint "sfx/streams/fc/" $dx_fullcombo_sound ".mogg"}}
         {fc_preview_clip set loop FALSE}
         {fc_preview_clip set volume -8}
         {fc_preview_clip play}
      }
      {if {== {$component selected_sym} os_nobg}
         {set $dx_fullcombo_sound dx_none}
      }
   )
   (on_cancel
      {$this show_state dxState_AudioSFX}
   )
)
